TestRunner = (
  | suite passes failures numAsserts |

  initializeOn: aSuite = (
    suite := aSuite.

    passes       := Vector new.
    failures     := Vector new.

    numAsserts := 0.
  )

  hasFailures     = ( ^ failures size > 0 )
  expectedPasses = ( ^ suite tests size )
  actualPasses   = ( ^ passes size )

  hasUnsupported  = ( ^ false )
  actualUnsupported = ( ^ 0 )

  run = (
    self reportPreRun.
    self runAllTests.
    self reportPostRun.
    self overviewReport.
  )

  countAssert = (
    numAsserts := numAsserts + 1.
  )

  numAsserts = (
    ^ numAsserts
  )

  reportPreRun = (
    ('TestSuite ' + suite name + ':') println.
    ('Tests: ' + suite tests size asString) println.
  )

  reportPostRun = (
    self hasFailures ifTrue: [
      ('Failures: ' + failures size asString) println
    ].
  )

  runAllTests = (
    suite tests do: [ :each |
        each run: self ].
  )

  overviewReport = (
    ('Tests passed: ' + passes size asString) println.

    self hasFailures ifTrue: [
        '------------------------------' println ].

    self hasFailures ifTrue: [
      ('Failures: ' + failures size asString) println.
      failures do: [:each |
        ('    ' + each key asString) println.
        ('        ' + each value asString) println ].
    ].
  )

  fail: aSignature because: aReason = (
    | pair |
    pair := Pair withKey: aSignature andValue: aReason.
    failures append: pair.
  )

  passed: aSignature = (
    passes append: aSignature
  )
)
